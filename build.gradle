buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" } // official gradle plugins
    }

    dependencies {
        // android gradle plugin
        classpath 'com.android.tools.build:gradle:1.3.1'
        // android analytics plugin
        classpath 'com.google.gms:google-services:1.3.1'
        // android command plugin
        classpath 'com.novoda:gradle-android-command-plugin:1.5.0'
        // static analysis
        classpath 'com.noveogroup.android:check:1.1.2'
        // google error-prone
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
        // publish to google play store
        classpath 'com.github.triplet.gradle:play-publisher:1.1.3'
        // check for plugin updates
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        // code coverage
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' } // official gradle plugins
    }

    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: 'com.github.ben-manes.versions'

    // 0.7.5.201505241946 - Caused by: java.io.IOException: Incompatible version 1007.
//    jacoco {
//        toolVersion = '0.7.5.201505241946'
//    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7

        // report all Java errors even if the IDE does not
        configure(options) {
            compilerArgs << '-Xlint:all' << '-Xlint:-options'
            deprecation = true
            encoding = 'UTF-8'
        }
    }

    // print errors from test in the terminal
    tasks.withType(Test) {
        testLogging {
            exceptionFormat 'full'
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
        }
    }
}

configure(project(':app').subprojects) {
    apply plugin: 'com.android.application'
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'android-command'
    apply plugin: 'com.noveogroup.android.check'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.github.triplet.play'

    android {
        defaultConfig {
            applicationId 'burrows.apps.example.template'
            minSdkVersion 9
            targetSdkVersion 23
            versionCode 1
            versionName '1.0'
            // for espresso tests
            testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        }

        sourceSets.main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }

        signingConfigs.debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        buildTypes.release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    // publishing to google play
    play {
        serviceAccountEmail = 'jaredsburrows@gmail.com'
        pk12File = file('key.p12')
        track = 'production' // or 'beta' or 'alpha'
    }

    dependencies {
        // Android Support Library for AppCompat
        compile 'com.android.support:appcompat-v7:23.0.1'

        // Android Support Library for the CardView
        compile 'com.android.support:cardview-v7:23.0.1'

        // Android Support Library for the RecyclerView
        compile 'com.android.support:recyclerview-v7:23.0.1'

        // Google Play Services - Ads
        compile 'com.google.android.gms:play-services-ads:7.5.0' // 7.8.0 - admob issue

        // Espresso Instrumentation Testing
        androidTestCompile 'com.android.support:support-annotations:23.0.1'
        androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
        androidTestCompile 'com.android.support.test:testing-support-lib:0.1'

        // Unit Tests
        testCompile project(':lib:test')
    }

    apply from: "${rootProject.getRootDir()}/common.gradle"
}

configure(project(':lib').subprojects) {
    apply plugin: 'com.android.library'
    apply plugin: 'com.noveogroup.android.check'
    apply plugin: 'net.ltgt.errorprone'

    dependencies {
        // testing
        if (name.contains('test')) {
            // Junit
            compile 'junit:junit:4.12'

            // Hamcrest Matchers for Junit
            compile 'org.hamcrest:hamcrest-integration:1.3'

            // Mockito
            compile 'org.mockito:mockito-core:2.0.31-beta'

            // EasyMock
            compile 'org.easymock:easymock:3.3.1'

            // PowerMock
            compile 'org.powermock:powermock-core:1.6.2'
            compile 'org.powermock:powermock-module-junit4:1.6.2'

            // PowerMock + EasyMock extension
            compile 'org.powermock:powermock-api-easymock:1.6.2'

            // Robolectric + Support v4
            compile 'org.robolectric:shadows-play-services:3.0'
            compile 'org.robolectric:shadows-support-v4:3.0'

            // AssertJ
            compile('com.squareup.assertj:assertj-android:1.1.0') { // 2/3.1.0 for java 8
                exclude module: 'support-v4'
                exclude module: 'support-annotations'
            }
        }

        // only apply testing project to all other projects
        if (!name.contains('test')) {
            testCompile project(':lib:test')
        }
    }

    android.lintOptions {
        disable 'UnusedResources', 'LogConditional'
    }

    apply from: "${rootProject.getRootDir()}/common.gradle"
}
