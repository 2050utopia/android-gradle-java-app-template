buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle:build-scan-plugin:1.4"
        classpath "com.android.tools.build:gradle:2.2.2"
        classpath "com.novoda:gradle-android-command-plugin:1.7.1"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.3.0"
        classpath "com.github.triplet.gradle:play-publisher:1.1.4"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.14.0"
    }
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.4"
    distributionType = Wrapper.DistributionType.ALL
}

apply from: rootProject.file("gradle/buildScan.gradle")
apply plugin: "com.android.application"
apply plugin: "android-command"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.ben-manes.versions"
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "burrows.apps.example.template"
        versionCode 1
        versionName "1.0"
        minSdkVersion 11
        targetSdkVersion 25
        // Espresso test runner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // Debug - testing and letting others use
    signingConfigs {
        debug {
            storeFile rootProject.file("./.buildscript/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
            proguardFile "proguard-rules.pro"
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries !Boolean.valueOf(System.getenv("TRAVIS"))
    }

    lintOptions {
        textOutput "stdout"
        checkAllWarnings true
        warningsAsErrors true
        disable "UnusedResources"              // Unused will be removed on release
        disable "IconExpectedSize"             // Using the material icons provided from Google
        disable "GoogleAppIndexingApiWarning"  // We might want to index our app later
        disable "InvalidPackage"               // Butterknife, Okio and Realm
        disable "ResourceType"                 // Annotation binding
        disable "GradleDependency"
        disable "NewerVersionAvailable"
    }

    testOptions.unitTests {
        all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

def supportLibraryVersion = "25.2.0"
def espressoVersion = "2.2.2"
def playServicesVersion = "10.2.0"
def powerMockVersion = "1.6.5"

dependencies {
    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"

    // Android Support Library for the CardView
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"

    // Android Support Library for the RecyclerView
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"

    // Google Play Services - Ads
    compile "com.google.android.gms:play-services-ads:${playServicesVersion}"
    compile "com.google.android.gms:play-services-basement:${playServicesVersion}"
    compile "com.google.android.gms:play-services-base:${playServicesVersion}"

    // Espresso Instrumentation Testing
    androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${espressoVersion}"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${espressoVersion}") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "support-v7"
        exclude module: "design"
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
    }

    // Junit
    testCompile "junit:junit:4.12"

    // Mockito
    testCompile "org.mockito:mockito-core:2.7.12"

    // EasyMock
    testCompile "org.easymock:easymock:3.4"

    // PowerMock
    testCompile "org.powermock:powermock-core:${powerMockVersion}"
    testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"

    // PowerMock + EasyMock extension
    testCompile "org.powermock:powermock-api-easymock:${powerMockVersion}"

    // Robolectric + Support v4
    testCompile "org.robolectric:shadows-support-v4:3.2.2"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"

    // AssertJ
    testCompile "com.android.support:support-annotations:${supportLibraryVersion}"
    testCompile "org.assertj:assertj-core:1.7.1"
}
