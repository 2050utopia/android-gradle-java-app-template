repositories {
    maven { url 'https://plugins.gradle.org/m2/' } // official gradle plugins
}

apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'net.ltgt.errorprone'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.kt3k.coveralls'

tasks.withType(JavaCompile) {
    // Show all warnings except boot classpath
    configure(options) {
        compilerArgs << '-Xlint:all' << '-Xlint:-options' << '-Werror' << '-Xdiags:verbose'
        deprecation = true
        encoding = 'UTF-8'
        // incremental = true // https://code.google.com/p/android/issues/detail?id=82411
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
    }
}

tasks.withType(JavaForkOptions) {
    // Prevent forked processes from stealing focus (on MacOS at least)
    jvmArgs '-Djava.awt.headless=true'
}

tasks.withType(Javadoc) {
    configure(options) {
        header = name
        author = true
        links(['http://docs.oracle.com/javase/8/docs/api/',
               'http://docs.oracle.com/javase/7/docs/api/'] as String[])
        linksOffline 'http://d.android.com/reference', "${android.sdkDirectory}/docs/reference"
        exclude '**/BuildConfig.java', '**/R.java'
        if (JavaVersion.current().isJava8Compatible()) {
            addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 11 // 14
        targetSdkVersion 23
        // Espresso test runner
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    // Use the "ndk-build" tool to build libs
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries project.hasProperty('debug')
        incremental project.hasProperty('debug')
    }

    packagingOptions {
        exclude '.readme'
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/.readme'
        exclude 'META-INF/readme.txt'
        exclude 'META-INF/README.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
		abortOnError false
        textReport true
        textOutput 'stdout'
        checkAllWarnings true
        warningsAsErrors true
        disable 'PrivateResource' // Use AppCompat resources
    }

    (android.hasProperty('applicationVariants') ? android.'applicationVariants' : android.'libraryVariants').all { variant ->
        task("pmd${variant.name.capitalize()}", type: Pmd, dependsOn: "assemble${variant.name.capitalize()}") {
            group 'Reporting'
            description "Generate ${variant.name.capitalize()} Pmd reports."

            ignoreFailures = true
            reports {
                xml.enabled = true
                html.enabled = true
            }

            source = files(variant.javaCompiler.source)
            classpath = files(configurations.compile.files)
        }

        task("jacoco${variant.name.capitalize()}Report", type: JacocoReport, dependsOn: "test${variant.name.capitalize()}UnitTest") {
            group 'Reporting'
            description "Generate ${variant.name.capitalize()} Jacoco coverage reports."

            reports {
                xml.enabled = true
                html.enabled = true
            }

            // variant.javaCompile.source does not work
            // traverses from starting point
            sourceDirectories = files(android.sourceSets.main.java.srcDirs)
            executionData = files("${buildDir}/jacoco/test${variant.name.capitalize()}UnitTest.exec")
            classDirectories = fileTree(
                    dir: variant.javaCompiler.destinationDir,
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/Manifest*.*',
                               'android/**/*.*',
                               '**/BuildConfig.*'])
        }

        task("findbugs${variant.name.capitalize()}", type: FindBugs, dependsOn: "assemble${variant.name.capitalize()}") {
            group 'Reporting'
            description "Generate ${variant.name.capitalize()} Findbugs reports."

            ignoreFailures = true
            reports {
                xml.enabled = false
                html.enabled = true
            }

            effort = 'max'
            reportLevel = 'low'
            source = files(variant.javaCompiler.source)
            classpath = files(configurations.compile.files)
            classes = fileTree(
                    dir: variant.javaCompile.destinationDir,
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/Manifest*.*',
                               'android/**/*.*',
                               '**/BuildConfig.*'])
        }

        task("checkstyle${variant.name.capitalize()}", type: Checkstyle, dependsOn: "assemble${variant.name.capitalize()}") {
            group 'Reporting'
            description "Generate ${variant.name.capitalize()} Checkstyle reports."

            ignoreFailures = true
            // TODO generate HTML, 'reports.html' is not available
            reports {
                xml.enabled = true
            }

            configFile = rootProject.file('gradle/checkstyle-hard.xml')
            source = files(android.sourceSets.main.java.srcDirs)
            classpath = files(configurations.compile.files)
        }
    }
}

